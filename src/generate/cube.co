request = require \request

color = (colors) ->
  {length} = colors
  if length is 0
    \A
  else if length is 1
    colors.0
  else
    \Y

normal = (packs) ->
  for pack, i of packs
    pack = for card of pack
      card{
        url: image_medium
        name
        rarity
      } <<<<
        color: color card.effective_cost
        cmc: card.mana_cost_converted or 0
    pack

module.exports = (cubeName, count, cb) ->
  options =
    uri: "http://tappedout.net/api/collection/cube/#cubeName/booster/?count=#count"
    json: true
  request options, (err, res, body) ->
    if err or res.statusCode is not 200
      console.log(err or "#{options.uri}: #{res.statusCode}")
      cb true
    else
      cb null normal body
