Player = require \./player

class Human extends Player
  (sock, @isHost) ->
    super!
    @attach sock

  attach: (sock) ->
    {@name} = sock
    @send = sock~send
    @send \set { @isHost, @main, pack: @packs.0 }
    sock.on \pick @~pick

    sock.on \name (@name) ~> @emit \name

    if @isHost
      # i want events to bubble
      sock.on \start ~> @emit \start it

  start: (set, round) ->
    super set
    @send \set { round }

  sendPack: ->
    pack = @packs.0

    if pack.length is 1
      @pick 0 true
    else
      @send \set { pack }

  pick: (index) ->
    super index
    @send \add @main[* - 1]

module.exports = Human
