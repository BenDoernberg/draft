Player = require \./player

class Human extends Player
  (@sock, @isHost) ->
    super!
    @ <<<< sock{ name, id }
    @attach sock

  attach: (sock) ->
    # TODO auto reconnect, d/c notice
    @sock.removeAllListeners!
    @sock = sock
    @send = sock~send
    @send \set { @isHost, @main, pack: @packs.0 }

    # i want bubbling, 1 arg w/ data + src, namespaces
    sock.on \pick @~pick
    sock.on \name (@name) ~> @emit \name
    sock.on \drop ~> @emit \drop @

    if @isHost
      sock.on \start ~> @emit \start it

  start: (set, round) ->
    super set
    @send \set { round }

  sendPack: ->
    pack = @packs.0

    if pack.length is 1
      @pick 0 true
    else
      @send \set { pack }

  pick: (index) ->
    super index
    @send \add @main[* - 1]

module.exports = Human
